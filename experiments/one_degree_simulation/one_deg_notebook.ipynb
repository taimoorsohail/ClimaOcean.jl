{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0aa3fc39-f670-4eb5-90ad-33d0d9efb1c4",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ClimaOcean\n",
    "using ClimaOcean.ECCO: ECCO4Monthly, NearestNeighborInpainting\n",
    "using OrthogonalSphericalShellGrids\n",
    "using Oceananigans\n",
    "using Oceananigans.Units\n",
    "using CFTime\n",
    "using Dates \n",
    "using Printf\n",
    "using CUDA: @allowscalar, device!\n",
    "\n",
    "using Oceananigans.Grids: znode"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f9268650-86f1-4928-9b75-136054499f58",
   "metadata": {},
   "outputs": [],
   "source": [
    "arch = GPU()\n",
    "\n",
    "#####\n",
    "##### Grid and Bathymetry\n",
    "#####\n",
    "\n",
    "Nx = 360\n",
    "Ny = 180\n",
    "Nz = 100\n",
    "\n",
    "z_faces = exponential_z_faces(; Nz, depth=5000, h=34)\n",
    "\n",
    "underlying_grid = TripolarGrid(arch;\n",
    "                               size = (Nx, Ny, Nz),\n",
    "                               z = z_faces,\n",
    "                               first_pole_longitude = 70,\n",
    "                               north_poles_latitude = 55)\n",
    "\n",
    "bottom_height = regrid_bathymetry(underlying_grid;\n",
    "                                  minimum_depth = 10,\n",
    "                                  interpolation_passes = 75,\n",
    "                                  major_basins = 2)\n",
    "\n",
    "# Open Gibraltar strait \n",
    "# TODO: find a better way to do this\n",
    "tampered_bottom_height = deepcopy(bottom_height)\n",
    "view(tampered_bottom_height, 102:103, 124, 1) .= -400\n",
    "\n",
    "grid = ImmersedBoundaryGrid(underlying_grid, GridFittedBottom(tampered_bottom_height); active_cells_map=true)\n",
    "\n",
    "#####\n",
    "##### Closures\n",
    "#####\n",
    "\n",
    "gm = Oceananigans.TurbulenceClosures.IsopycnalSkewSymmetricDiffusivity(κ_skew=1000, κ_symmetric=1000)\n",
    "catke = ClimaOcean.OceanSimulations.default_ocean_closure()\n",
    "viscous_closure = Oceananigans.TurbulenceClosures.HorizontalScalarDiffusivity(ν=2000)\n",
    "\n",
    "closure = (gm, catke, viscous_closure)\n",
    "\n",
    "#####\n",
    "##### Restoring\n",
    "#####\n",
    "\n",
    "restoring_rate  = 1 / 2days\n",
    "z_below_surface = @allowscalar znode(1, 1, grid.Nz, grid, Center(), Center(), Face())\n",
    "\n",
    "mask = LinearlyTaperedPolarMask(southern=(-80, -70), northern=(70, 90), z=(z_below_surface, 0))\n",
    "\n",
    "dates = DateTimeProlepticGregorian(1993, 1, 1) : Month(1) : DateTimeProlepticGregorian(1993, 11, 1)\n",
    "temperature = ECCOMetadata(:temperature; dates, version=ECCO4Monthly(), dir=\"./\")\n",
    "salinity    = ECCOMetadata(:salinity;    dates, version=ECCO4Monthly(), dir=\"./\")\n",
    "\n",
    "# inpainting = NearestNeighborInpainting(30) should be enough to fill the gaps near bathymetry\n",
    "FT = ECCORestoring(arch, temperature; grid, mask, rate=restoring_rate, inpainting=NearestNeighborInpainting(50))\n",
    "FS = ECCORestoring(arch, salinity;    grid, mask, rate=restoring_rate, inpainting=NearestNeighborInpainting(50))\n",
    "forcing = (T=FT, S=FS)\n",
    "\n",
    "#####\n",
    "##### Ocean simulation\n",
    "##### \n",
    "\n",
    "momentum_advection = VectorInvariant()\n",
    "tracer_advection   = Centered(order=2)\n",
    "\n",
    "free_surface = SplitExplicitFreeSurface(grid; substeps=30)\n",
    "\n",
    "# Should we add a side drag since this is at a coarser resolution?\n",
    "ocean = ocean_simulation(grid; momentum_advection, tracer_advection,\n",
    "                         free_surface, forcing)\n",
    "\n",
    "set!(ocean.model, T=ECCOMetadata(:temperature; dates=first(dates)),\n",
    "                  S=ECCOMetadata(:salinity;    dates=first(dates)))\n",
    "\n",
    "#####\n",
    "##### Sea ice simulation\n",
    "#####\n",
    "\n",
    "sea_ice = sea_ice_simulation(grid)\n",
    "\n",
    "set!(sea_ice.model.ice_thickness,     ECCOMetadata(:sea_ice_thickness;     dates=first(dates)); inpainting=NearestNeighborInpainting(0))\n",
    "set!(sea_ice.model.ice_concentration, ECCOMetadata(:sea_ice_concentration; dates=first(dates)); inpainting=NearestNeighborInpainting(0))\n",
    "\n",
    "#####\n",
    "##### Atmospheric forcing\n",
    "#####\n",
    "\n",
    "radiation  = Radiation(arch)\n",
    "atmosphere = JRA55PrescribedAtmosphere(arch; backend=JRA55NetCDFBackend(20))\n",
    "\n",
    "#####\n",
    "##### Coupled simulation\n",
    "#####\n",
    "\n",
    "coupled_model = OceanSeaIceModel(ocean, sea_ice; atmosphere, radiation) \n",
    "simulation = Simulation(coupled_model; Δt=1minutes, stop_time=2*365days)\n",
    "\n",
    "#####\n",
    "##### Run it!\n",
    "##### \n",
    "\n",
    "wall_time = Ref(time_ns())\n",
    "\n",
    "function progress(sim)\n",
    "    ocean = sim.model.ocean\n",
    "    sea_ice = sim.model.sea_ice\n",
    "    u, v, w = ocean.model.velocities\n",
    "    T = ocean.model.tracers.T\n",
    "    h = sea_ice.model.ice_thickness\n",
    "    Tmax = maximum(interior(T))\n",
    "    Tmin = minimum(interior(T))\n",
    "    hmax = maximum(interior(h))\n",
    "    umax = (maximum(abs, interior(u)),\n",
    "            maximum(abs, interior(v)),\n",
    "            maximum(abs, interior(w)))\n",
    "\n",
    "    step_time = 1e-9 * (time_ns() - wall_time[])\n",
    "\n",
    "    @info @sprintf(\"Time: %s, n: %d, Δt: %s, max|u|: (%.2e, %.2e, %.2e) m s⁻¹, extrema(T): (%.2f, %.2f) ᵒC, max(h): %.2f m, wall time: %s \\n\",\n",
    "                   prettytime(sim), iteration(sim), prettytime(sim.Δt),\n",
    "                   umax..., Tmax, Tmin, hmax, prettytime(step_time))\n",
    "\n",
    "     wall_time[] = time_ns()\n",
    "\n",
    "     return nothing\n",
    "end\n",
    "\n",
    "add_callback!(simulation, progress, IterationInterval(10))\n",
    "\n",
    "run!(simulation)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.1",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "name": ""
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
